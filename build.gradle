plugins {
    id 'de.dfki.mary.component' version '0.3.2'
    id 'maven-publish'
    id 'signing'
}

group 'de.dfki.mary'
version '0.2.0'
description 'Upper Sorbian language component for MaryTTS'

ext {
    url = 'https://github.com/marytts/marytts-lang-hsb'
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

marytts {
    component {
        name = 'UpperSorbian'
        packageName = 'marytts.language.hsb'
        configBaseClass = 'LanguageConfig'
        config from: 'component.yaml'
    }
}

dependencies {
    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.9.0'
    runtimeOnly group: 'de.dfki.mary', name: 'marytts-lexicon-hsb', version: '0.2.0'
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.register('run', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set 'marytts.server.Mary'
    systemProperties << ['log4j.logger.marytts': 'DEBUG,stderr']
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 only'
                        url = 'https://spdx.org/licenses/LGPL-3.0-only.html'
                    }
                }
                developers {
                    developer {
                        id = 'psibre'
                        name = 'Ingmar Steiner'
                    }
                    developer {
                        id = 'aStereoID'
                        name = 'Astrid Schmiedel'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/marytts/marytts-lang-hsb.git'
                    developerConnection = 'scm:git:ssh://git@github.com:marytts/marytts-lang-hsb.git'
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            url isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}
